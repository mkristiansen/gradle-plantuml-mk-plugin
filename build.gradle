// * * * * * * * * * * * *
// Plugins
// * * * * * * * * * * * *

plugins {
    id 'groovy'
    id 'java-gradle-plugin'

    // Publishing publicly
    id 'com.gradle.plugin-publish' version '0.9.7'

    // Publishing to Artifactory
    id 'com.jfrog.artifactory' version '4.4.18'
    id 'maven-publish'
}

// * * * * * * * * * * * *
// Dependencies
// * * * * * * * * * * * *

repositories {
    jcenter()
}

dependencies {
    compile 'commons-io:commons-io:2.4'
    compile 'net.sourceforge.plantuml:plantuml:8059'
    testCompile 'junit:junit:4+'
}

// * * * * * * * * * * * *
// Project configuration
// * * * * * * * * * * * *

project.description     = 'Gradle plugin to render ASCII PlantUML files into images'
project.group           = 'io.kristiansen.gradle'
project.version         = '0.0.1'

project.ext.name        = 'gradle-plantuml-plugin'
project.ext.pluginId    =  project.group + '.PlantUMLPlugin'
project.ext.scm         = 'scm:git@github.com:mkristiansen/gradle-plantuml-mk-pluginp.git'
project.ext.url         = 'https://github.com/mkristiansen/gradle-plantuml-mk-plugin'

// Configuration for: java-gradle-plugin
gradlePlugin {
    plugins {
        PlantUMLPlugin {
            id = project.ext.pluginId
            implementationClass = 'io.kristiansen.gradle.plantuml.PlantUMLPlugin'
        }
    }
}

// * * * * * * * * * * * *
// Publishing
// * * * * * * * * * * * *

// Consult the accompanied blog post for information on publishing the plugin.
// A link can be found in the README file.

// Publishing to plugins.gradle.org
// --------------------------------

// Configuration for: com.gradle.plugin-publish
pluginBundle {
    website = project.url
    vcsUrl = project.scm
    tags = ['plantuml', 'plugin']

    plugins {
        PlantUML {
            displayName = 'Render ascii PlantUML files into images'
            description = project.description
            id          = project.pluginId
        }
    }
}

// Publishing to Artifactory
// -------------------------

// Configuration for com.jfrog.artifactory
artifactory {
    contextUrl = project.rtfUrl
    publish {
        repository {
            repoKey     = project.rtfRepo
            username    = project.rtfUser
            password    = project.rtfSecret
            maven       = true
        }
        defaults{
            publications("pluginPublication") // Publication defined below
        }
    }
}

// Plugin publication
publishing {
    publications {
        pluginPublication (MavenPublication) {
            from        components.java
            groupId     project.group
            artifactId  project.name
            version     project.version
        }
    }
}
